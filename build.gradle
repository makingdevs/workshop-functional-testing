import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath "com.github.samueltbrown:gradle-cucumber-plugin:0.9"
  }
}

ext {
  groovyVersion = '2.4.14'
  gebVersion = '2.1'
  htmlUnitVersion = '2.52.0'
  seleniumVersion = '3.11.0'
  cucumberJvmVersion = '1.2.5'
  chromeDriverVersion = '2.37'
  phantomJsVersion = '2.1.1'
  firefoxDriverVersion = '0.20.0'
}

apply plugin: 'groovy'
apply plugin: "com.github.samueltbrown.cucumber"
apply from: "osSpecificDownloads.gradle"

repositories {
  mavenCentral()
  jcenter()
}

sourceCompatibility = 11
targetCompatibility = 11

dependencies {
  compile "org.codehaus.groovy:groovy-all:$groovyVersion"
  testCompile "info.cukes:cucumber-core:$cucumberJvmVersion"
  testCompile "info.cukes:cucumber-groovy:$cucumberJvmVersion"
  testCompile "info.cukes:cucumber-junit:$cucumberJvmVersion"
  testCompile "org.gebish:geb-core:$gebVersion"
  testCompile "org.seleniumhq.selenium:selenium-htmlunit-driver:$htmlUnitVersion"
  testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
  testCompile "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
  testCompile "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
  testCompile("com.codeborne:phantomjsdriver:1.4.4") {
    // phantomjs driver pulls in a different selenium version
    transitive = false
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.9'
}


cucumber {
  def tagsToRun = System.getProperty("cucumber.tags")
  formats = ['pretty','json:build/cucumber.json','junit:build/cucumber.xml','html:build/reports/cucumber']
  tags = tagsToRun ? [tagsToRun] : []
}

tasks.cucumber {
  dependsOn unzipChromeDriver, unzipPhantomJs

  def chromeDriverFilename = Os.isFamily(Os.FAMILY_WINDOWS) ? "chromedriver.exe" : "chromedriver"
  def phantomJsFilename = Os.isFamily(Os.FAMILY_WINDOWS) ? "phantomjs.exe" : "bin/phantomjs"
  def firefoxFilename = Os.isFamily(Os.FAMILY_WINDOWS) ? "geckodriver.exe" : "geckodriver"

  def properties = [
    "webdriver.chrome.driver": new File(unzipChromeDriver.outputs.files.singleFile, chromeDriverFilename).absolutePath,
    "phantomjs.binary.path": new File(unzipPhantomJs.outputs.files.singleFile, phantomJsFilename).absolutePath,
    "webdriver.gecko.driver": new File(unzipFirefox.outputs.files.singleFile, firefoxFilename).absolutePath,
    //"geb.cucumber.step.packages": "pages",
    "geb.env": System.getProperty("geb.env")
  ]

  if(System.getProperty("geb.saucelabs.file"))
    properties."geb.saucelabs.browser" = new File(System.getProperty("geb.saucelabs.file"))?.text

  jvmOptions.systemProperties(properties)
}
